_basename=jicofo
_tag_version=4857
_tag=jitsi-meet_${_tag_version}
_version=1.0+612

pkgname=${_basename}
pkgver=${_version}
pkgrel=1
pkgdesc="Jicofo"
arch=('any')
url="https://jitsi.org/jitsi-meet/"
license=('Apache')
depends=("java-runtime" "bash")
optdepends=("prosody")
makedepends=(
        "java-environment"
        "unzip" "maven"
)
options=('!strip')
backup=(
  "etc/${pkgname}/config"
  "etc/${pkgname}/logging.properties"
  "etc/${pkgname}/sip-communicator.properties"
)
source=(
        "$pkgname-$pkgver.tar.gz::https://github.com/jitsi/${pkgname}/archive/stable/${_tag}.tar.gz"
        "config"
        "sip-communicator.properties"
        "service"
        "sysusers.conf"
        "tmpfiles.conf"
)
groups=('jitsi-meet', 'celogeek')
noextract=(
        "$pkgname-$pkgver.tar.gz"
)

prepare() {
  [ -d "$pkgname-$pkgver" ] && return 0
  mkdir "$pkgname-$pkgver"
  tar xzf "$pkgname-$pkgver.tar.gz" --strip 1 -C "$pkgname-$pkgver"
}

build() {
        cd "$pkgname-$pkgver"
        mvn clean
        mvn package -DskipTests -Dassembly.skipAssembly=true
        mvn dependency:copy-dependencies -DincludeScope=runtime
}

package() {
        cd "$srcdir/$pkgname-$pkgver"
        
        DESTDIR="${pkgdir}/usr/lib/${pkgname}"
        CONFDIR="${pkgdir}/etc/${pkgname}"

        install -Dm644 -t "${DESTDIR}/lib" target/dependency/*
        install -Dm644 target/jicofo*.jar "${DESTDIR}/jicofo.jar"

	install -dm750 "${CONFDIR}"
        install -Dm640 -t "${CONFDIR}" "lib/logging.properties"
        install -Dm755 -t "${DESTDIR}" "resources/jicofo.sh"

        cd "$srcdir"
        install -Dm640 -t "${CONFDIR}" "config" "sip-communicator.properties"
        install -Dm644 "service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

        install -Dm644 "sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/$pkgname.conf"
        install -Dm644 "tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/$pkgname.conf"
}
sha256sums=('84f50d9e7b3561054b342cb776613ed3ba9f49e0198971b16fe2ecc97a8cfa5b'
            '537d93f8f8e046c536139f0a75acdf86e2f2bf35d562a012e26a0bcfe6446eaa'
            'f295f5f8ee13edd019defc037c60e04c6ea2d30e69cc4a896c010b8570f5efab'
            '0fd4da92b61bf6299d414f660159fa9ce47caf3b002f75fda82f18ee0e594695'
            '0681e97ca1e06d8ea7bdec0a874c6fc7a6ea84628923005130cd444547a1b440'
            '2e8ee4baac220977a84496cc040e55ed644aa305a1db895914dfc62215a89a65')
