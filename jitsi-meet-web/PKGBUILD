_basename=jitsi-meet
_pkgname=web
_tag_version=4857
_tag=${_basename}_${_tag_version}
_version=1.0.4289

pkgname=${_basename}-${_pkgname}
pkgver=${_version}
pkgrel=1
pkgdesc="Jitsi Meet Web"
arch=('any')
url="https://jitsi.org/jitsi-meet/"
license=('Apache')
depends=()
optdepends=("nginx")
makedepends=(
        "python" "python2"
        "nodejs" "npm"
)
options=('!strip')
backup=(
  "etc/webapps/${pkgname}/logging_config.js"
  "etc/webapps/${pkgname}/config.js"
  "etc/webapps/${pkgname}/interface_config.js"
)
source=(
        "$pkgname-$pkgver.tar.gz::https://github.com/jitsi/jitsi-meet/archive/stable/${_tag}.tar.gz"
)
noextract=(
    "$pkgname-$pkgver.tar.gz"
)
groups=('jitsi-meet', 'celogeek')

prepare() {
  [ -d "$pkgname-$pkgver" ] && return 0
  mkdir "$pkgname-$pkgver"
  tar xzf "$pkgname-$pkgver.tar.gz" --strip 1 -C "$pkgname-$pkgver"
}

build() {
        cd "$pkgname-$pkgver"
        npm install
        make
        make source-package
}

package() {
        cd "$srcdir/$pkgname-$pkgver"

        DESTDIR="${pkgdir}/usr/share/webapps/${pkgname}"
        CONFDIR="${pkgdir}/etc/webapps/${pkgname}"

        install -d "$DESTDIR"
        install -d "$CONFDIR"

        tar xjvf "jitsi-meet.tar.bz2" -C "$DESTDIR" --strip 1

        for c in $(ls "node_modules/i18n-iso-countries/langs")
        do
            install -m644 "node_modules/i18n-iso-countries/langs/${c}" "${DESTDIR}/lang/countries-${c}"
        done

        find "$DESTDIR" -type f -execdir sed -i "s#${srcdir}##g" "{}" \;
        find "$DESTDIR" -type d -exec chmod 755 {} \;

        for i in interface_config.js logging_config.js config.js
        do
                install -Dm644 "$DESTDIR/${i}" "$CONFDIR/${i}"
                ln -sf "/etc/webapps/${pkgname}/${i}" "$DESTDIR/${i}"
        done

        install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}" doc/debian/jitsi-meet/jitsi-meet.example doc/debian/jitsi-meet/jitsi-meet.example-apache config.js
        sed -i 's@/usr/share/jitsi-meet@/usr/share/webapps/jitsi-meet-web@' "${pkgdir}/usr/share/doc/${pkgname}/"*
        
        chown -R root:root "${pkgdir}"
}
sha256sums=('f15f9060ddbc650724563d12bfc8f85c88d303ea823fbb402b462d11e23473cd')
