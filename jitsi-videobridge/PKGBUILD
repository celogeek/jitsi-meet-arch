_basename=jitsi
_pkgname=videobridge
_tag_version=4857
_tag=jitsi-meet_${_tag_version}
_version=2.1+273+g072dd44b

pkgname=${_basename}-${_pkgname}
pkgver=${_version}
pkgrel=1
pkgdesc="Jitsi Meet Videobridge"
arch=('any')
url="https://jitsi.org/jitsi-meet/"
license=('Apache')
depends=("java-runtime" "bash")
optdepends=("prosody")
makedepends=(
        "java-environment"
        "unzip" "maven"
)
options=('!strip')
backup=(
  "etc/${pkgname}/config"
  "etc/${pkgname}/log4j2.xml"
  "etc/${pkgname}/logging.properties"
  "etc/${pkgname}/sip-communicator.properties"
)
source=(
        "$pkgname-$pkgver.tar.gz::https://github.com/jitsi/${pkgname}/archive/stable/${_tag}.tar.gz"
        "config"
        "sip-communicator.properties"
        "service"
        "sysusers.conf"
        "tmpfiles.conf"
)
noextract=(
    "$pkgname-$pkgver.tar.gz"
)
groups=('jitsi-meet', 'celogeek')

prepare() {
  [ -d "$pkgname-$pkgver" ] && return 0
  mkdir "$pkgname-$pkgver"
  tar xzf "$pkgname-$pkgver.tar.gz" --strip 1 -C "$pkgname-$pkgver"
}

build() {
        cd "$pkgname-$pkgver"
        mvn clean
        mvn package -DskipTests -Dassembly.skipAssembly=true install
        mvn dependency:copy-dependencies -DincludeScope=runtime
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

        DESTDIR="${pkgdir}/usr/lib/${pkgname}"
        CONFDIR="${pkgdir}/etc/${pkgname}"

        install -Dm644 -t "${DESTDIR}/lib" target/dependency/* lib/videobridge.rc
        install -Dm644 target/${pkgname}*.jar "${DESTDIR}/${pkgname}.jar"
        install -Dm755 -t "${DESTDIR}" "resources/jvb.sh"

	install -dm750 "${CONFDIR}"
        install -Dm640 -t "${CONFDIR}" "lib/logging.properties" "config/log4j2.xml" "config/callstats-java-sdk.properties"
	sed -i 's@logs@/var/log/jitsi-videobridge@' "${CONFDIR}/log4j2.xml"

	install -Dm644 "config/20-jvb-udp-buffers.conf" "${pkgdir}/etc/sysctl.d/${pkgname}.conf"

	cd "$srcdir"
        install -Dm640 -t "${CONFDIR}" "config" "sip-communicator.properties"
        install -Dm644 "service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
        install -Dm644 "sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/$pkgname.conf"
        install -Dm644 "tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/$pkgname.conf"
}
sha256sums=('9cc7420c492d31744499b619472c5a7145f012956291058ee20dc865cfc9cf58'
            '5c79dc1e1f5ee04eba3da987c488fc53cb6e4348345cab05ab0ed6d7000f3d9d'
            'cc9fbf77497bce3c9673b2d144928f11cdd0c0823940c2b60c8369a2f086b9b7'
            '1e128b74c791d184f4634ce5eaed6f7f367642e7ba6f513da1387132841ed3de'
            '998cbc64def56ab98080ff7150dd0913a5e10325cd2b038cf3db14baf8cb19fc'
            '4dd8141e71bbf73b39cf242902e1c1a84a38dc3160ff89e76883d999af10541c')
